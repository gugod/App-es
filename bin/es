#!/usr/bin/perl

use strict;
use warnings;

use App::es;

sub _usage {
    require Pod::Usage;
    Pod::Usage::pod2usage();
}

# get args
my $cmd = shift or do {
    print "es - version $App::es::VERSION\n\n";
    _usage();
};
_usage() if $cmd =~ /^(?:help|-h(elp)?|--h(elp)?)$/;
die "illegal command\n" unless App::es->is_command( $cmd );

binmode STDOUT, ":utf8";

my $app = App::es->new_with_options;

my @params = @ARGV;
utf8::decode($_) for @params;

my $es = $app->es;

my @aliases = @{ $app->_get_elastic_search_aliases };

$app->validate_params($cmd => \@params);

( $cmd eq 'ls'       ) and $app->command_ls       ( @params );
( $cmd eq 'ls-types' ) and $app->command_ls_types ( @params );
( $cmd eq 'ls-aliases' ) and $app->command_ls_aliases ( @params );
( $cmd eq 'get-mapping'  ) and $app->command_get_mapping ( @params );
( $cmd eq 'get-settings' ) and $app->command_get_settings( @params );
( $cmd eq 'put-mapping'  ) and $app->command_put_mapping ( @params );
( $cmd eq 'put-settings' ) and $app->command_put_settings( @params );
( $cmd eq 'reindex'  )   and $app->command_reindex( @params );
( $cmd eq 'create'   ) and $app->command_create( @params );
( $cmd eq 'delete'   ) and $app->command_delete( @params );
( $cmd eq 'put'      ) and $app->command_put( @params );
( $cmd eq 'search'   ) and $app->command_search( @params );
( $cmd eq 'scan'     ) and $app->command_scan( @params );
( $cmd eq 'get'      ) and $app->command_get( @params );
( $cmd eq 'alias'    ) and $app->command_alias( @params );
( $cmd eq 'unalias'  ) and $app->command_unalias( @params );
( $cmd eq 'copy'     ) and $app->command_copy( @params );

exit 0;


__END__

=head1 NAME

es -- The command line client of ElasticSearch.

=head1 SYNOPSIS

    # List document count / size per index / type
    es ls [<index_name_substring>]
    es ls-types <index>
    es ls-aliases <index> # list aliases of an index

    # Create / delete an index
    es create <index>
    es delete <index>

    es get-settings <index>
    es get-mapping <index>
    es reindex <index_from> <index_to>

    # Indexing / getting a document
    es put <index> <doc> # doc needs to be a JSON file
    es get <index> <type> <doc_id>

    # Search by a field
    es search <index> <type> <field>:<search string> [<size>]

    # Scan (scrolled search an index)
    es scan <index> <type> <search string> [<size>]

    # Aliasing
    es alias   <index> <alias>
    es unalias <index> <alias>

See `perldoc es` for more info.

=head1 DESCRIPTION

This program can be used to basic manipulation for day-to-day development need
with ElasticSearch server. The default server is C<localhost:9200>, to use
different server, you can change it with C<ELASTIC_SEARCH_SERVERS> environment variable.

    ELASTIC_SEARCH_SERVERS=search-server.company.com:9200

You may supply a list of servers seperate by comma

    ELASTIC_SEARCH_SERVERS=search-server-1.company.com:9200,search-server-2.company.com:9200

The list of servers are used in a round-robin manner, which is the default
behaviour of the underlying L<ElasticSearch> perl module.

=head1 COMMANDS

=head2 ls [<index sub-string>]

List all indices. Or if a sub-string is provided, list only matching ones.

=head2 ls-types <index>

List all document type of the given index.

=head2 ls-aliases <index>

List all aliases of the given index.

=head2 create <index> [--mapping mapping.json] [--settings settings.json]

Create the index. Optionally takes settings and mapping from JSON files. These
JSON files can be accquired by invoking L</get-settings> or L</get-mapping>
command on existing indices.

=head2 delete <index>

Delete the index

=head2 reindex <index_from> <index_to>

Reindex all documents in <index_from> into <index_to>. It is required that
<index_to> is created first.

=head2 get-mapping <index>

Get the mapping of an existing index. It prints the mapping in JSON text.

=head2 get-settings <index>

Get the settings of an existing index. It prints the settings in JSON text.
Something looks like this:

    {
       "index.number_of_replicas" : "1",
       "index.number_of_shards" : "5",
       "index.version.created" : "191099"
    }

=head2 alias <index> <alias>

Add an alias for the index.

=head2 unalias <index> <alias>

Remove an alias to the index.

=head2 put <index> <type> <doc>

Put the content of doc into the index.  <type> is the the documentation type.
<doc> must be a valid JSON file.

=head2 get <index> <type> <doc_id>

Print the specic document as JSON.

=head2 search <index> <type> <field>:<query_string>

Perform a simple text query. Need to provide field name and a query string.

=head2 scan <index> <type> <query_string>

Perform a scan of an index for a text.

=head1 AUTHORS

Mickey Nasriachi E<lt>mickey75@gmail.comE<gt>

Kang-min Liu E<lt>gugod@gugod.orgE<gt>


=head1 ACKNOWLEDGMENT

This module was originally developed for Booking.com. With approval from
Booking.com, this module was generalized and published on CPAN, for which the
authors would like to express their gratitude.

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2013 by Mickey Nasriachi

Copyright (C) 2013 by Kang-min Liu

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

